[SQLite Library Example]

Shows how to use an external library to
operate a SQLite database.

[libraries/db]
command {
    - python3
    - "-m"
    - "sqlitelib"
    - "database.sqlite3"
}

[commands/run]

scope "Create the table" {
    db mutation {{
        CREATE TABLE IF NOT EXISTS example (
            id INTEGER PRIMARY KEY,
            created_at TEXT NOT NULL UNIQUE
        )
    }} | print "create table> "
}

scope "Print the content of the table" {
    db query {{
        SELECT *
        FROM example
    }} | print "*> "
}

scope "Add a new entry" {
    unixtime
        | :: to.isotime
        | >> {print "now="}
        | list
        | as values

    db mutation {{
        INSERT INTO example
        (created_at)
        VALUES (?)
    }} -- (values = $values)
}

scope "Count how many entries we already have" {
    db query {{
        SELECT count(*) as n
        FROM example
    }} | print "count> "
}

scope "Random Python command" {
    db print "This was printed by Python" -- (file = stderr)
}
