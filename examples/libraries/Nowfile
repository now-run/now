[Supervisor Example]

Runs a child process and communicate with it in a
efficient manner.

[libraries/cat]
command {
    - now
    - :lib
    - :f
    - "$script_dir/cat.now"
}

[libraries/test_lib]
command {
    - "$script_dir/library.sh"
    - {
        arg1 "argument-one"
    }
}

[libraries/sum_lib]
command {
    - now
    - :lib
    - :f
    - sum.now
}

[procedures/sum]
parameters {
    a {
        type integer
    }
    b {
        type integer
    }
}

log "procedures/sum $a $b"
return ([to.integer $a] + [to.integer $b])

[commands/run]
description "Call some procedures from external library."

test_lib start 1 (2 = 3) [dict (4 = 5) (6 = 7)] [list 8 9 10] | log "response:"
test_lib err
    ! "self inflicted error" {log "Error (as expected): $error"}
    ! * {log "Uncaught error: [${error}]"}

log "Proceeding..."

# The library is going to call a procedure from THIS document:
test_lib test_calls 1 2
    | >> {log "test_calls 1 2: "}
    | :: eq 3
    | :: assert

[commands/ue]

log "START"
test_lib unhandled_error
    ! * {log "error>>> $error"}
test_lib test_calls 3 4 | :: eq 7 | :: assert
log "FINISH"

[commands/sum]

log "START"
sum_lib sum 2 3 | log "2+3="
sum_lib sum 5 7 | log "5+7="
sum_lib sum 17 19 | log "17+19="
log "END"

[commands/cat]

log "START"
o "alfa\nbeta\ngama"
| cat cat
| log

log "---"

o "one\ntwo\nthree\nfour"
| cat cat
| log

log "END"
